(define-module (zem ui minibuffer-view)
  #:use-module ((zem api font) #:prefix f:)
  #:use-module ((zem api renderer) #:prefix r:)
  #:use-module (zem core faces)
  #:use-module (zem ui view)
  #:use-module ((zem ui style) #:prefix style:)
  #:use-module (emacsy emacsy)
  #:use-module (ice-9 match)
  #:use-module (oop goops)
  #:export (<minibuffer-view>))

(define-class <minibuffer-view> (<view>)
  (message #:init-form "" #:accessor minibuffer-message))

(define (get-text-y-offset)
  (floor (/ (f:get-font-height (face-attribute 'default ':font)) 4.0)))

(define-method (view:draw (view <minibuffer-view>))
  (match-let (((x . y) (view:pos view))
              ((w . h) (view:size view))
              (font (face-attribute 'default ':font))
              (foreground (face-attribute 'default ':foreground)))
             (draw-view-background view style:background-color)
             (r:add-text font
                         (cons (+ x (car style:padding)) (+ y (f:get-font-height font)))
                         (minibuffer-message view)
                         foreground
                         (- w (car style:padding)))))

(define-method (view:get-size-request (view <minibuffer-view>))
  (set! (minibuffer-message view) (buffer:buffer-string minibuffer))
  (match-let (((w . h) (r:text-size-hint (face-attribute 'default ':font)
                                         (minibuffer-message view)
                                         (- (car (view:size view))
                                            (* 2 (car style:padding))))))
             (cons w (+ h (get-text-y-offset)))))
